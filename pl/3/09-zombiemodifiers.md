---
title: Modyfikatory Zombi
actions:
  - 'sprawdźOdpowiedź'
  - 'podpowiedź'
requireLogin: prawda
material:
  editor:
    language: sol
    startingCode:
      "zombiehelper.sol": |
        pragma solidity ^0.4.19;
        
        import "./zombiefeeding.sol";
        
        contract ZombieHelper is ZombieFeeding {
        
        modifier aboveLevel(uint _level, uint _zombieId) {
        require(zombies[_zombieId].level >= _level);
        _;
        }
        
        // Zacznij tutaj
        
        }
      "zombiefeeding.sol": |
        pragma solidity ^0.4.19;
        
        import "./zombiefactory.sol";
        
        contract KittyInterface {
        function getKitty(uint256 _id) external view returns (
        bool isGestating,
        bool isReady,
        uint256 cooldownIndex,
        uint256 nextActionAt,
        uint256 siringWithId,
        uint256 birthTime,
        uint256 matronId,
        uint256 sireId,
        uint256 generation,
        uint256 genes
        );
        }
        
        contract ZombieFeeding is ZombieFactory {
        
        KittyInterface kittyContract;
        
        function setKittyContractAddress(address _address) external onlyOwner {
        kittyContract = KittyInterface(_address);
        }
        
        function feedAndMultiply(uint _zombieId, uint _targetDna, string _species) public {
        require(msg.sender == zombieToOwner[_zombieId]);
        Zombie storage myZombie = zombies[_zombieId];
        _targetDna = _targetDna % dnaModulus;
        uint newDna = (myZombie.dna + _targetDna) / 2;
        if (keccak256(_species) == keccak256("kitty")) {
        newDna = newDna - newDna % 100 + 99;
        }
        _createZombie("NoName", newDna);
        }
        
        function feedOnKitty(uint _zombieId, uint _kittyId) public {
        uint kittyDna;
        (,,,,,,,,,kittyDna) = kittyContract.getKitty(_kittyId);
        feedAndMultiply(_zombieId, kittyDna, "kitty");
        }
        
        }
      "zombiefactory.sol": |
        pragma solidity ^0.4.19;
        
        import "./ownable.sol";
        
        contract ZombieFactory is Ownable {
        
        event NewZombie(uint zombieId, string name, uint dna);
        
        uint dnaDigits = 16;
        uint dnaModulus = 10 ** dnaDigits;
        uint cooldownTime = 1 days;
        
        struct Zombie {
        string name;
        uint dna;
        uint32 level;
        uint32 readyTime;
        }
        
        Zombie[] public zombies;
        
        mapping (uint => address) public zombieToOwner;
        mapping (address => uint) ownerZombieCount;
        
        function _createZombie(string _name, uint _dna) internal {
        uint id = zombies.push(Zombie(_name, _dna, 1, uint32(now + cooldownTime))) - 1;
        zombieToOwner[id] = msg.sender;
        ownerZombieCount[msg.sender]++;
        NewZombie(id, _name, _dna);
        }
        
        function _generateRandomDna(string _str) private view returns (uint) {
        uint rand = uint(keccak256(_str));
        return rand % dnaModulus;
        }
        
        function createRandomZombie(string _name) public {
        require(ownerZombieCount[msg.sender] == 0);
        uint randDna = _generateRandomDna(_name);
        randDna = randDna - randDna % 100;
        _createZombie(_name, randDna);
        }
        
        }
      "ownable.sol": |
        /**
        * @title Ownable
        * @dev The Ownable contract has an owner address, and provides basic authorization control
        * functions, this simplifies the implementation of "user permissions".
        */
        contract Ownable {
        address public owner;
        
        event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);
        
        /**
        * @dev The Ownable constructor sets the original `owner` of the contract to the sender
        * account.
        */
        function Ownable() public {
        owner = msg.sender;
        }
        
        
        /**
        * @dev Throws if called by any account other than the owner.
        */
        modifier onlyOwner() {
        require(msg.sender == owner);
        _;
        }
        
        
        /**
        * @dev Allows the current owner to transfer control of the contract to a newOwner.
        * @param newOwner The address to transfer ownership to.
        */
        function transferOwnership(address newOwner) public onlyOwner {
        require(newOwner != address(0));
        OwnershipTransferred(owner, newOwner);
        owner = newOwner;
        }
        
        }
    answer: >
      pragma solidity ^0.4.19;
      import "./zombiefeeding.sol";
      contract ZombieHelper is ZombieFeeding {
      modifier aboveLevel(uint _level, uint _zombieId) { require(zombies[_zombieId].level >= _level); _; }
      function changeName(uint _zombieId, string _newName) external aboveLevel(2, _zombieId) { require(msg.sender == zombieToOwner[_zombieId]); zombies[_zombieId].name = _newName; }
      function changeDna(uint _zombieId, uint _newDna) external aboveLevel(20, _zombieId) { require(msg.sender == zombieToOwner[_zombieId]); zombies[_zombieId].dna = _newDna; }
      }
---
Teraz użyjmy modyfikatora `aboveLevel` aby stworzyć jakieś funkcje.

Nasza gra będzie miała dla ludzi pewne zachęty do tego, aby podnosić poziom ich Zombiaków:

- Dla poziomu 2 i wyższego, użytkownicy będą mieli możliwość zmiany imienia Zombiaka.
- Dla poziomu 20 i wyższego, użytkownicy będą mieli możliwość nadać im wybrane DNA.

Zaimplementujemy te funkcje poniżej. Tutaj jest przykład kodu z poprzedniej lekcji:

    // Mapowanie do przechowywania wieku usera:
    mapping (uint => uint) public age;
    
    // Modyfikator, który wymaga, aby user był w starszym wieku niż określony:
    modifier olderThan(uint _age, uint _userId) {
      require (age[_userId] >= _age);
      _;
    }
    
    // Musi być starszy niż 16 lat (przynajmniej w USA)
    function driveCar(uint _userId) public olderThan(16, _userId) {
      // Jakaś logika funkcji
    }
    

## Wypróbujmy zatem

1. Stwórz funkcję, o nazwie `changeName`. Będzie ona pobierała 2 argumenty: `_zombieId` (`uint`) i `_newName` (`string`), zróbmy ją `external`. Powinna mieć modyfikator `aboveLevel` i przekazywać `2` dla parametru `_level`. (Nie zapomnij przekazaż również `_zombieId`).

2. W tej funkcji, najpierw zweryfikujemy czy `msg.sender` jest równy `zombieToOwner[_zombieId]`. Użyj do tego wyrażenia `require`.

3. Potem, funkcja powinna ustawiać `zombies[_zombieId].name` równe `_newName`.

4. Stwórz inną funkcję o nazwie `changeDna` poniżej `changeName`. Jej definicja i zawartość będzie prawie identyczna do `changeName`, z wyjątkiem drugiego argumentu, którym będzie `_newDna` (`uint`) i przekaże on `20` dla parametru `_level` w `aboveLevel`. I oczywiście, powinien ustawić `dna` Zombiaka jako `_newDna` zamiast ustawiać imię Zombi.